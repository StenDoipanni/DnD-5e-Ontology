
### This query differs from the previous one for setting the parameter fx:use-rdfs-member to "true".
### This allows to use the rdfs:member property instead of rdf:_1, rdf:_2, etc.
### The ontological module created is about the "Druid" Character Class.

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dnd: <https://w3id.org/dnd5e/>


#SELECT DISTINCT ?name ?proficiency ?numSkills ?spellAbility ?autolevel

CONSTRUCT {
  
  ### CLASS RELATED
  
  ?dndclass a owl:NamedIndividual , owl:Class ;
    dnd:proficientIn ?classProficiency ;
    dnd:hitDice ?hitDice ;
    dnd:castsWith ?castingAbility ;
    dnd:includesLevel ?classLevel ;
    dnd:armorCompetenceIn ?lightArmor , ?mediumArmor , ?heavyArmor , ?shields ;
    dnd:weaponCompetenceIn ?weaponType .

    
    ### GENERAL D&D TRIPLES
    
    dnd:Ability a owl:Class .
	?classProficiency a owl:Class , owl:NamedIndividual ; a dnd:Ability .
    ?hitDice a owl:Class , owl:NamedIndividual ; a dnd:DiceType .
}

WHERE {
  SERVICE <x-sparql-anything:> {
    fx:properties fx:use-rdfs-member true .
    fx:properties fx:location "https://raw.githubusercontent.com/kinkofer/FightClub5eXML/main/Sources/PlayersHandbook/class-druid-phb.xml" .
    
    ?root a xyz:compendium ; rdfs:member ?class .
    ?class rdfs:member ?nameC , ?hdC , ?proficiencyC , ?numSkillsC , ?spellAbilityC , ?autolevelC .
    ?nameC a xyz:name ; rdfs:member ?name .
    ?hdC a xyz:hd ; rdfs:member ?hd .
    ?proficiencyC a xyz:proficiency ; rdfs:member ?proficiency .
    ?numSkillsC a xyz:numSkills ; rdfs:member ?numSkills .
    ?spellAbilityC a xyz:spellAbility ; rdfs:member ?spellAbility .
    ?autolevelC a xyz:autolevel ; rdfs:member ?autolevel ; xyz:level ?lv .
    ?autolevel rdfs:member ?feature .
    ?feature rdfs:member ?text2 .
    
    
    BIND(IRI(CONCAT("https://w3id.org/dnd5e/",str(?name))) AS ?dndclass)
    BIND(IRI(CONCAT("https://w3id.org/dnd5e/d",str(?hd))) AS ?hitDice)
    BIND(IRI(CONCAT("https://w3id.org/dnd5e/",str(?numSkills))) AS ?classSkill)
    BIND(IRI(CONCAT("https://w3id.org/dnd5e/",str(?spellAbility))) AS ?castingAbility)
    BIND(IRI(CONCAT(str(?dndclass),"Lv")) AS ?classlv)
    BIND(IRI(CONCAT(str(?classlv),str(?autolevel))) AS ?classLevel)
    BIND(IF(CONTAINS(?text2, "• Armor:"), ?text2, false) AS ?armorType)
    BIND(IF(CONTAINS(?armorType, "light"), IRI("https://w3id.org/dnd5e/LightArmor"), false) AS ?lightArmor)
    BIND(IF(CONTAINS(?armorType, "medium"), IRI("https://w3id.org/dnd5e/MediumArmor"), false) AS ?mediumArmor)
    BIND(IF(CONTAINS(?armorType, "heavy"), IRI("https://w3id.org/dnd5e/HeavyArmor"), false) AS ?heavyArmor)
    BIND(IF(CONTAINS(?armorType, "shield"), IRI("https://w3id.org/dnd5e/Shields"), false) AS ?shields)
    BIND(IF(CONTAINS(?text2, "• Weapons:"), ?text2, false) AS ?weapon)
    BIND(REPLACE(STR(?weapon), "• Weapons: ", "") AS ?modWeapon)
    
    OPTIONAL {
      SERVICE <x-sparql-anything:> {
          fx:properties fx:content ?modWeapon .
          fx:properties fx:txt.split ", " .
          FILTER(isLiteral(?w))
          FILTER(strlen(str(?modWeapon))>0)
          ?a ?b ?w
          BIND(REPLACE(STR(?w), " ", "_") AS ?w2) .
          BIND(IRI(CONCAT("https://w3id.org/dnd5e/",str(?w2))) AS ?weaponType)
        }
      }   
    
      OPTIONAL {
        SERVICE <x-sparql-anything:> {
          fx:properties fx:content ?proficiency .
          fx:properties fx:txt.split ", " .
          FILTER(isLiteral(?cProf))
          FILTER(strlen(str(?proficiency))>0)
          ?a ?b ?cProf
          BIND(REPLACE(STR(?cProf), " ", "_") AS ?modClassProficiency) .
          BIND(IRI(CONCAT("https://w3id.org/dnd5e/",str(?modClassProficiency))) AS ?classProficiency)
          
  }
  }      
  }
}
